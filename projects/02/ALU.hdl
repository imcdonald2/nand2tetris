// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //Prep x with zx, then with nx, to get nxout
    Mux16(a[0..15]=x, b[0..15]=false, sel=zx, out[0..15]=zxout);
    Not16(in[0..15]=zxout, out[0..15]=notx);
    Mux16(a[0..15]=zxout, b[0..15]=notx, sel=nx, out[0..15]=nxout);

    //Prep y with zy, then with ny, to get nyout
    Mux16(a[0..15]=y, b[0..15]=false, sel=zy, out[0..15]=zyout);
    Not16(in[0..15]=zyout, out[0..15]=noty);
    Mux16(a[0..15]=zyout, b[0..15]=noty, sel=ny, out[0..15]=nyout);

    //Create wires for x+y, x and y, as well as prepared by the f func
    Add16(a[0..15]=nxout, b[0..15]=nyout, out[0..15]=xplusy);
    And16(a[0..15]=nxout, b[0..15]=nyout, out[0..15]=xandy);
    Mux16(a[0..15]=xandy, b[0..15]=xplusy, sel=f, out[0..15]=fout);

    //Prep a not of fout
    Not16(in[0..15]=fout, out[0..15]=notfout);

    Mux16(a[0..15]=fout, b[0..15]=notfout, sel=no, out[0..7]=lfout, out[8..15]=hfout, out[15]=ng, out=out);

    Or8Way(in[0..7]=lfout, out=lout);
    Or8Way(in[0..7]=hfout, out=hout);
    Or(a=lout, b=hout, out=forout);
    Not(in=forout, out=zr);
}